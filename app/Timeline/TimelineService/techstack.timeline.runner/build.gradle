plugins {
    id 'java'
    id 'org.springframework.boot' version '2.1.4.RELEASE'
}


sourceCompatibility = '12'
targetCompatibility = '12'

repositories {
    mavenCentral()
    jcenter()
}

configurations {
    developmentOnly
    runtimeOnly {
        extendsFrom developmentOnly
    }
}

dependencies {
    implementation project(":$openApiModule")
    implementation 'org.springframework.boot:spring-boot-starter-actuator:2.1.4.RELEASE'
    implementation 'org.springframework.boot:spring-boot-starter-webflux:2.1.4.RELEASE'

    testImplementation 'org.junit.jupiter:junit-jupiter:5.4.2'
    testImplementation ("org.springframework.boot:spring-boot-starter-test:2.1.4.RELEASE") {
        exclude group: 'junit', module: 'junit'
        exclude group: 'org.hamcrest'
        exclude group: 'org.skyscreamer'
    }
    testImplementation 'io.projectreactor:reactor-test:3.1.0.RELEASE'
    testImplementation 'org.hamcrest:hamcrest-core:2.1'

    developmentOnly("org.springframework.boot:spring-boot-devtools:2.1.4.RELEASE")
}

bootRun {
    if (project.hasProperty('args')) {  // -Pargs="arg1 arg2"
        args project.args.split('\\s+')   //  Apply arguments for main method
    }

    // -Pjms    Enable JavaMission Control recording
    if (project.hasProperty('jmc')) {  // -Pjmc
        jvmArgs '-Dcom.sun.management.jmxremote -Dcom.sun.management.jmxremote.port=8999',
                '-Dcom.sun.management.jmxremote.ssl=false',
                '-Dcom.sun.management.jmxremote.authenticate=false',
                '-XX:+UnlockCommercialFeatures',
                '-XX:+FlightRecorder',
                "-XX:FlightRecorderOptions=defaultrecording=true,dumponexit=true,dumponexitpath=$buildDir/flightRecorderResult.jfr"
    }
}

springBoot {
    buildInfo()
}

bootJar {
    launchScript()
}

test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
    }
}